plugins {
    id 'org.parchmentmc.writtenbooks' version '0.5.1.1' apply false
}

project.group 'org.parchmentmc.feather'

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'org.parchmentmc.writtenbooks'

    group rootProject.group

    repositories {
        maven {
            name 'ParchmentMC'
            url 'https://ldtteam.jfrog.io/artifactory/parchmentmc/'
        }
    }

    writtenbooks {
        githubRepo = 'ParchmentMC/Feather'
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testImplementation 'com.google.guava:guava:30.1.1-jre'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications.withType(MavenPublication).configureEach {
            pom {
                organization {
                    name = "ParchmentMC"
                    url = "https://github.com/ParchmentMC"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
            }
        }
    }
}
