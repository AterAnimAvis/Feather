plugins {
    id 'org.parchmentmc.writtenbooks' version '0.4.0' apply false
    id 'io.spring.dependency-management' version '1.0.1.RELEASE' apply false
}

project.group 'org.parchmentmc.feather'

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'org.parchmentmc.writtenbooks'

    group rootProject.group

    repositories {
        maven {
            name 'ParchmentMC'
            url 'https://ldtteam.jfrog.io/artifactory/parchmentmc/'
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testImplementation 'com.google.guava:guava:30.1.1-jre'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications.withType(MavenPublication).all {
            pom {
                url = "https://github.com/ParchmentMC/Feather"
                organization {
                    name = "ParchmentMC"
                    url = "https://github.com/ParchmentMC"
                }
                scm {
                    url = "https://github.com/ParchmentMC/Feather"
                    connection = "scm:git:git://github.com/ParchmentMC/Feather.git"
                    developerConnection = "scm:git:git@github.com/ParchmentMC/Feather.git"
                }
                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/ParchmentMC/Feather/issues"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
            }
        }
    }
}
