plugins {
    id 'org.parchmentmc.writtenbooks' version '0.5.1.1' apply false
}

project.group 'org.parchmentmc.feather'

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'org.parchmentmc.writtenbooks'

    group rootProject.group

    repositories {
        maven {
            name 'ParchmentMC'
            url 'https://ldtteam.jfrog.io/artifactory/parchmentmc/'
        }
    }

    writtenbooks {
        githubRepo = 'ParchmentMC/Feather'
    }

    dependencies {
        testFixturesApi libs.junit.api

        testRuntimeOnly libs.junit.engine
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
        withJavadocJar()
        withSourcesJar()
    }

    // Disable publishing of test fixtures
    components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
    components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

    publishing {
        publications.withType(MavenPublication).configureEach {
            pom {
                organization {
                    name = "ParchmentMC"
                    url = "https://github.com/ParchmentMC"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
            }
        }
    }
}
